Do llData.$cols.$add(lrConference.CF_SEQ)
Do llData.$cols.$add(lrConference.CF_TITLE)
Do llData.$cols.$add(lrConference.CF_DATE_FROM)
Do llData.$cols.$add(lrConference.CF_TITLE_CODE)

Do llData.$cols.$add(lrSpeaker.PSN_SEQ)
Do llData.$cols.$add(lrSpeaker.PSN_FIRST_NAME)
Do llData.$cols.$add(lrSpeaker.PSN_SURNAME)
Do llData.$cols.$add(lrSpeaker.PSN_EMAIL)
Do llData.$cols.$add('SpeakerSort',kCharacter,kSimplechar,50)

Do llData.$cols.$add(lrTopics.TPC_SEQ)
Do llData.$cols.$add(lrTopics.TPC_TITLE)
Do llData.$cols.$add(lrTopics.TPC_ABSTRACT)
Do llData.$cols.$add(lrTopics.TPC_DESC)

Do llData.$cols.$add($cinst.ARR_MAX_BOOKINGS)
Do llData.$cols.$add('attended',kInteger,k32bitint)
Do llData.$cols.$add('percent',kNumber,k0dp)

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

Begin statement
Sta:select  CF_SEQ, CF_TITLE, CF_DATE_FROM, CF_TITLE_CODE
Sta:, PSN_SEQ, PSN_FIRST_NAME, PSN_SURNAME, PSN_EMAIL, upper(concat(PSN_SURNAME,', ',PSN_FIRST_NAME))
Sta:, TPC_SEQ, TPC_TITLE, TPC_ABSTRACT, TPC_DESC
Sta:,  sum(ARR_MAX_BOOKINGS)
Sta:, ( select count(*)
Sta:from [lrAttendees.$servertablenames()]
Sta:join [$cinst.$servertablenames()] on ARA_ARR_REF = ARR_SEQ
Sta:where ARR_TPC_REF = TPC_SEQ and ARR_CF_REF = CF_SEQ
Sta:)
Sta:from [$cinst.$servertablenames()]
Sta:join [lrConference.$servertablenames()] on CF_SEQ = ARR_CF_REF
Sta:join [lrTopics.$servertablenames] on TPC_SEQ = ARR_TPC_REF
# exclude the Organising Committee
Sta:join [lrSpeaker.$servertablenames] on PSN_SEQ = ARR_PSN_REF and PSN_SEX <> 'O'
Sta:where  CF_GO_REF = @[trGroupOrg.GO_SEQ]
Sta:group by CF_SEQ, CF_TITLE, CF_DATE_FROM, CF_TITLE_CODE
Sta:, PSN_SEQ, PSN_FIRST_NAME, PSN_SURNAME, PSN_EMAIL, upper(concat(PSN_SURNAME,', ',PSN_FIRST_NAME))
Sta:, TPC_SEQ, TPC_TITLE, TPC_ABSTRACT, TPC_DESC
End statement

Do $cinst.$executesqlandfetch(llData) Returns lbOK

If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

Do llData.$sort($ref.SpeakerSort,0,$ref.CF_DATE_FROM,1,$ref.CF_SEQ,0,$ref.TPC_TITLE,0)

Calculate llData.$line as 0
While $Loop(llData)
	If llData.ARR_MAX_BOOKINGS<>0
		Calculate llData.percent as llData.attended/llData.ARR_MAX_BOOKINGS*100
	End If
End While

Quit method llData