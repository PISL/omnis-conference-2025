If not(ibCompareUsers)
	Quit method
End If
If irPreviousRecord.USR_SEQ
	If irCurrentRecord.USR_SEQ
		Calculate icDistinctRolesCurrent as ''
		Calculate icDistinctRolesPrevious as ''
		Calculate icCommonRoles as ''
		
		Calculate lcDelimiter as '* '
		
		# we could do this with SQL using intersect and subtract but we have all the data here in lists already...
		Do ilPreviousUserRoles.$search(0)
		Do ilUserRoles.$search(0)
		# highlight the PER_LABEL entries common to both lists
		Calculate ilPreviousUserRoles.$line as 0
		While $Loop(ilPreviousUserRoles)
			Do ilUserRoles.$search($ref.ROL_NAME=ilPreviousUserRoles.ROL_NAME,1,0,1,0) Returns lbFound
			Calculate ilPreviousUserRoles.[ilPreviousUserRoles.$line].$selected as lbFound
		End While
		# anything unselected in each list is unique to that list
		Calculate ilUserRoles.$line as 0
		While $Loop(ilUserRoles)
			If ilUserRoles.[ilUserRoles.$line].$selected
				Calculate icCommonRoles as con(icCommonRoles,pick(icCommonRoles<>'','',kCr),lcDelimiter,ilUserRoles.ROL_NAME)
			Else
				Calculate icDistinctRolesCurrent as con(icDistinctRolesCurrent,pick(icDistinctRolesCurrent<>'','',kCr),lcDelimiter,ilUserRoles.ROL_NAME)
			End If
		End While
		
		Calculate ilPreviousUserRoles.$line as 0
		While $Loop(ilPreviousUserRoles)
			If ilPreviousUserRoles.[ilPreviousUserRoles.$line].$selected
				# already recorded in icCommonPermissions
			Else
				Calculate icDistinctRolesPrevious as con(icDistinctRolesPrevious,pick(icDistinctRolesPrevious<>'','',kCr),lcDelimiter,ilPreviousUserRoles.ROL_NAME)
			End If
		End While
		
		Do ilUserRoles.$search(0)
		Do ilPreviousUserRoles.$search(0)
		
		#     !!! stb
		Calculate $cinst.$objs.label_DistinctCurrentRoles.$text as con('Distinct roles: ',irCurrentRecord.USR_NAME)
		Calculate $cinst.$objs.label_CommonRoles.$text as con('Common roles ')
		Calculate $cinst.$objs.label_DistinctPreviousRoles.$text as con('Distinct roles: ',irPreviousRecord.USR_NAME)
		
	End If
End If