If not(ibCompareUsers)
	Quit method
End If

# with smaller width forms the text on each line may overlap and we need a delimiter of some sort to highlight this
Calculate lcNewLine as '* '

If irPreviousRecord.ROL_SEQ
	If irCurrentRecord.ROL_SEQ
		Calculate icDistinctUsersCurrent as ''
		Calculate icDistinctUsersPrevious as ''
		Calculate icCommonUsers as ''
		
		# we could do this with SQL using intersect and subtract but we have all the data here in lists already...
		Do ilPreviousRoleUsers.$search(0)
		Do ilRoleUsers.$search(0)
		# highlight the PER_LABEL entries common to both lists
		Calculate ilPreviousRoleUsers.$line as 0
		While $Loop(ilPreviousRoleUsers)
			Do ilRoleUsers.$search($ref.USR_NAME=ilPreviousRoleUsers.USR_NAME,1,0,1,0) Returns lbFound
			Calculate ilPreviousRoleUsers.[ilPreviousRoleUsers.$line].$selected as lbFound
		End While
		# anything unselected in each list is unique to that list
		Calculate ilRoleUsers.$line as 0
		While $Loop(ilRoleUsers)
			If ilRoleUsers.[ilRoleUsers.$line].$selected
				Calculate icCommonUsers as con(icCommonUsers,pick(icCommonUsers<>'','',kCr),lcNewLine,ilRoleUsers.USR_NAME)
			Else
				Calculate icDistinctUsersCurrent as con(icDistinctUsersCurrent,pick(icDistinctUsersCurrent<>'','',kCr),lcNewLine,ilRoleUsers.USR_NAME)
			End If
		End While
		
		Calculate ilPreviousRoleUsers.$line as 0
		While $Loop(ilPreviousRoleUsers)
			If ilPreviousRoleUsers.[ilPreviousRoleUsers.$line].$selected
				# already recorded in icCommonPermissions
			Else
				Calculate icDistinctUsersPrevious as con(icDistinctUsersPrevious,pick(icDistinctUsersPrevious<>'','',kCr),lcNewLine,ilPreviousRoleUsers.USR_NAME)
			End If
		End While
		
		Do ilRoleUsers.$search(0)
		Do ilPreviousRoleUsers.$search(0)
	End If
End If