If not(ibComparePermissions)
	Quit method
End If

# with smaller width forms the text on each line may overlap and we need a delimiter of some sort to highlight this
Calculate lcNewLine as '* '

If irPreviousRecord.ROL_SEQ
	If irCurrentRecord.ROL_SEQ
		Calculate icDistinctPermissionsCurrent as ''
		Calculate icDistinctPermissionsPrevious as ''
		Calculate icCommonPermissions as ''
		
		# we could do this with SQL using intersect and subtract but we have all the data here in lists already...
		Do ilPreviousRolePermissions.$search(0)
		Do ilRolePermissions.$search(0)
		# highlight the PER_LABEL entries common to both lists
		Calculate ilPreviousRolePermissions.$line as 0
		While $Loop(ilPreviousRolePermissions)
			Do ilRolePermissions.$search($ref.PER_LABEL=ilPreviousRolePermissions.PER_LABEL,1,0,1,0) Returns lbFound
			Calculate ilPreviousRolePermissions.[ilPreviousRolePermissions.$line].$selected as lbFound
		End While
		# anything unselected in each list is unique to that list
		Calculate ilRolePermissions.$line as 0
		While $Loop(ilRolePermissions)
			If ilRolePermissions.[ilRolePermissions.$line].$selected
				Calculate icCommonPermissions as con(icCommonPermissions,pick(icCommonPermissions<>'','',kCr),lcNewLine,ilRolePermissions.PER_DESC_EN)
			Else
				Calculate icDistinctPermissionsCurrent as con(icDistinctPermissionsCurrent,pick(icDistinctPermissionsCurrent<>'','',kCr),lcNewLine,ilRolePermissions.PER_DESC_EN)
			End If
		End While
		
		Calculate ilPreviousRolePermissions.$line as 0
		While $Loop(ilPreviousRolePermissions)
			If ilPreviousRolePermissions.[ilPreviousRolePermissions.$line].$selected
				# already recorded in icCommonPermissions
			Else
				Calculate icDistinctPermissionsPrevious as con(icDistinctPermissionsPrevious,pick(icDistinctPermissionsPrevious<>'','',kCr),lcNewLine,ilPreviousRolePermissions.PER_DESC_EN)
			End If
		End While
		
		Do ilRolePermissions.$search(0)
		Do ilPreviousRolePermissions.$search(0)
		
		#     !!! stb
		Calculate $cinst.$objs.PagedPane.$objs.pagedPaneComparisons.$objs.label_DistinctCurrentPermissions.$text as con('Distinct permissions: ',irCurrentRecord.ROL_NAME)
		Calculate $cinst.$objs.PagedPane.$objs.pagedPaneComparisons.$objs.label_CommonPermissions.$text as con('Common permissions ')
		Calculate $cinst.$objs.PagedPane.$objs.pagedPaneComparisons.$objs.label_DistinctPreviousPermissions.$text as con('Distinct permissions: ',irPreviousRecord.ROL_NAME)
		
		Calculate $cinst.$objs.PagedPane.$objs.pagedPaneComparisons.$objs.label_DistinctCurrentUsers.$text as con('Distinct users: ',irCurrentRecord.ROL_NAME)
		Calculate $cinst.$objs.PagedPane.$objs.pagedPaneComparisons.$objs.label_CommonUsers.$text as con('Common users ')
		Calculate $cinst.$objs.PagedPane.$objs.pagedPaneComparisons.$objs.label_DistinctPreviousUsers.$text as con('Distinct users: ',irPreviousRecord.ROL_NAME)
		
	End If
End If