If $ctask.trGroupOrg.GO_SEQ>0
	# proceed
Else
	Quit method kFalse
End If

If tlDatabases.DAM='WebService'
	# pass parameters to web service
	Do toWeb.$callFetch($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(pcINT_PROD_CODE),$cinst.$type,0,ilRESTextensions))
	If toWeb.irResponse.httpStatusCode=200
		# the result should be in this object ## however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method kTrue
	Else
		Quit method kFalse
	End If
End If

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

Calculate pcINT_PROD_CODE as upp(pcINT_PROD_CODE)

Begin statement
Sta:select [$cinst.$selectnames()]
Sta:, [lrInternalData.$selectnames()]

Sta:from [$cinst.$servertablenames]
Sta:join [lrInternalData.$servertablenames] on PID_PRD_REF = PRD_SEQ
Sta:left outer join [lrReferenceLocal.$servertablenames] on ( PID_TAX_BAND = RFL_VALUE and rfl_class='VAT_RATE' )

Sta:where PRD_GO_REF = @[$ctask.trGroupOrg.GO_SEQ]
Sta:and      upper(PRD_INT_PRODCODE) = @[pcINT_PROD_CODE]

End statement

If pbReturnCount
	Do $cinst.$executesqlandfetchinto(lnCount) Returns lbOK
Else
	Do $cinst.$executesqlandfetch() Returns lbOK
End If
If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

Quit method lbOK