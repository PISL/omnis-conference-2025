If tlDatabases.DAM='WebService'
	# $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) ) ## result will be returned in pFieldReference, row() contains the parameters for the call
	Do toWeb.$callFetch($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(pnEO_REF,pcSuppProdCode),$cinst.$type,0,ilRESTextensions))
	If toWeb.irResponse.httpStatusCode=200
		# the result should be in this object ## however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method kTrue
	Else
		Quit method kFalse
	End If
End If

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

# out of Products
# a.  purchased ## they have a supplier number
# ## PRD_SUPP_CODE will have a value
# b.  by-product ## is a product we make but not for sale - it is for input into another process
# ## PRD_SUPP_CODE will be null
# c.  product ## something that we sell

# this method provides a. only

Begin statement
Sta:select [$cinst.$selectnames()]
Sta:from [$cinst.$servertablenames]
Sta:, [lrSupplier.$servertablenames]
# supplier products always linked to supplier record
Sta:where PRD_EO_REF = EO_SEQ
Sta:and    PRD_GO_REF = @[$ctask.trGroupOrg.GO_SEQ]
If tcCurrentLibrary<>'CONFERENCE'
	Sta:and PRD_META = @[inMeta]
End If
Sta:and      EO_SEQ = @[pnEO_REF]

Sta:and upper(PRD_SUPP_PRODCODE) like @[pcSuppProdCode]

Sta:order by PRD_NAME
End statement

Do $cinst.$executesqlandfetch() Returns lbOK
If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

Quit method lbOK