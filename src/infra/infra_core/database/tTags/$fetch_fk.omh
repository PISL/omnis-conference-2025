
# Do method InitDebugVars
If pForeignPrefix<>''&(pForignKey<>''|pForignKey<>0)
	# continue
Else
	Quit method
End If

If tlDatabases.DAM='WebService'
	# pass parameters to web service
	# $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) ) ## result will be returned in pFieldReference, row() contains the parameters for the call
	Calculate ldtStart as #D
	Do toWeb.$callFetch($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(pForignKey,pForeignPrefix,pOtherCriteria),$cinst.$type,0,ilRESTextensions))
	Calculate inServiceMS as ddiff(kCentiSecond,ldtStart,#D)*10
	Calculate inRESTserverMS as toWeb.inServerTime
	If toWeb.irResponse.httpStatusCode=200
		# the result should be in this object ## however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method kTrue
	Else
		Quit method kFalse
	End If
End If

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

Calculate icStatementType as 'SELECT'

# Calculate lnDataType as $cinst.$cols.[pForeignPrefix].$coltype

Calculate inID as pForignKey
# Calculate icForeignPrefix as pForeignPrefix
Calculate lcCriteria as con("where tag_pk = @[inID] and tag_table_prefix=",kSq,pForeignPrefix,kSq)

If pOtherCriteria<>''
	If left(upp(pOtherCriteria),4)='AND '
		# Sta: [pOtherCriteria]
		# leave as is
	Else
		# Sta: and [pOtherCriteria]
		Calculate pOtherCriteria as con(' and ',pOtherCriteria)
	End If
	Calculate lcCriteria as con(lcCriteria,' ',pOtherCriteria)
End If

Do $cinst.$Sql(,lcCriteria,0,0)

Do $cinst.$executesqlandfetch() Returns lbOK
If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

# Do method populateDisplayName

Quit method lbOK