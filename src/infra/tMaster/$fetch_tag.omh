If $ctask.tlDatabases.DAM='WebService'
	# pass parameters to web service
	# $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) ) ## result will be returned in pFieldReference, row() contains the parameters for the call
	Calculate ldtStart as #D
	Do $ctask.toWeb.$callFetch($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(pcTag),ldMaxTimestamp.$type,0,ilRESTextensions))
	Calculate inServiceMS as ddiff(kCentiSecond,ldtStart,#D)*10
	Calculate inRESTserverMS as $ctask.toWeb.inServerTime
	If $ctask.toWeb.irResponse.httpStatusCode=200
		# the result should be in this object ## however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method ldMaxTimestamp
	Else
		Quit method kFalse
	End If
End If

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

Begin statement
Sta: select [$cinst.$selectnames()]
Sta: from [$cinst.$servertablenames()]
Sta: join [lrTags.$servertablenames] on [icPrimarykey] = TAG_PK and TAG_TABLE_PREFIX = @[icPrefix] and TAG_VALUE = @[pcTag]
End statement

If icForeignCols<>''
	Calculate lcCriteria as con("tag_value = ",kSq,pcTag,kSq)
	Do $cinst.$Sql(,lcCriteria,0,0)
Else
	# the above Statement will be executed
End If

Do $cinst.$executesqlandfetch() Returns lbOK

If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

Quit method lbOK