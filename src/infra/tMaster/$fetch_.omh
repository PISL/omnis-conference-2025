Calculate inCountSelected as 0
Do method InitDebugVars
Calculate pWhereClause as trim(pWhereClause)
If (pWhereClause<>'')
	# continue
Else If pFetchAll
	# fetching all rows - continue
Else
	Quit method
End If

If $ctask.tlDatabases.DAM='WebService'
	# pass parameters to web service
	# $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) ) ## result will be returned in pFieldReference, row() contains the parameters for the call
	Calculate ldtStart as #D
	If pbReturnCount
		Do $ctask.toWeb.$callFetch(lnCount,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(pWhereClause,pFetchAll,pbReturnCount),$cinst.$type,0,ilRESTextensions))
		Calculate inCountSelected as lnCount
	Else
		Do $ctask.toWeb.$callFetch($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,row(pWhereClause,pFetchAll,pbReturnCount),$cinst.$type,0,ilRESTextensions))
	End If
	Calculate inServiceMS as ddiff(kCentiSecond,ldtStart,#D)*10
	Calculate inRESTserverMS as $ctask.toWeb.inServerTime
	If $ctask.toWeb.irResponse.httpStatusCode=200
		# the result should be in this object ## however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method kTrue
	Else
		Quit method kFalse
	End If
End If

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

Calculate icStatementType as 'SELECT'

Begin statement
If pbReturnCount
	Sta:select count(*)
Else
	Sta:select [$cinst.$selectnames()]
End If
Sta:from [$cinst.$servertablenames]
If mid(upp(pWhereClause),1,6)='WHERE '
	Sta:[pWhereClause]
Else If mid(upp(pWhereClause),1,4)='AND '
	Sta:where [mid(pWhereClause,5,9999999)]
Else If pWhereClause<>''
	Sta:where [pWhereClause]
End If

If $cinst.$extraquerytext<>''
	Calculate lcJoinClause as $cinst.$extraquerytext
	If mid(upp(lcJoinClause),1,6)='WHERE '
		Sta:and [mid(lcJoinClause,7,99999)]
	Else If mid(upp(lcJoinClause),1,4)='AND '
		Sta:[lcJoinClause]
	Else
		Sta:and [lcJoinClause]
	End If
End If

End statement
If pbReturnCount
	Do $cinst.$executesqlandfetchinto(lnCount) Returns lbOK
Else
	Do $cinst.$executesqlandfetch() Returns lbOK
End If

If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

Do method populateDisplayName

If pbReturnCount
	Quit method lnCount
Else
	Quit method lbOK
End If