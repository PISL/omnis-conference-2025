If $ctask.trGroupOrg.GO_SEQ>0
	# proceed
Else
	Quit method kFalse
End If

If tlDatabases.DAM='WebService'
	# pass parameters to web service
	# $callService (pFieldReference, row(TableClassName, CurrentMethodName, p1,...,pn) ) ## result will be returned in pFieldReference, row() contains the parameters for the call
	Do toWeb.$callFetch($cinst,irErrorSQL,row($cinst.$class().$name,$cmethod().$name,#NULL,$cinst.$type,0,ilRESTextensions))
	If toWeb.irResponse.httpStatusCode=200
		# the result should be in this object ## however there may be a SQL error
		If irErrorSQL.NativeErrorText<>''
			Quit method kFalse
		End If
		Quit method kTrue
	Else
		Quit method kFalse
	End If
End If

Do $cinst.$ImmediateSession(loSession,loStatement) Returns lbImmediateSession
If lbImmediateSession
	Do $cinst.$SetSession(loSession,loStatement)
Else If not(ibGoodConnectionToDb)
	Quit method kFalse
End If

Begin statement
Sta:select distinct [$cinst.$selectnames()]
Sta:from [$cinst.$servertablenames]
Sta:join [lrLinkUserToGroupOrg.$servertablenames()] on UGO_USR_REF = USR_SEQ
Sta:join [lrGroupOrg.$servertablenames()] on UGO_GO_REF = GO_SEQ
# Sta: {where  UGO_GO_REF = @[$ctask.trGroupOrg.GO_SEQ]}
If pcSearchString<>''
	Sta:where  (
	Sta:USR_NAME ilike '%[pcSearchString]%'
	Sta:or USR_REAL_NAME ilike '%[pcSearchString]%'
	Sta:or USR_EMAIL ilike '%[pcSearchString]%'
	Sta:or USR_INITIALS ilike '%[pcSearchString]%'
	
	Sta:or GO_NAME_FULL ilike '%[pcSearchString]%'
	Sta:or GO_NAME_SHORT ilike '%[pcSearchString]%'
	Sta:)
	
	Calculate lbPrecedentSet as kTrue
End If
If pcStatus<>''
	If lbPrecedentSet
		Sta:and USR_STATUS = @[pcStatus]
	Else
		Sta:where USR_STATUS = @[pcStatus]
		Calculate lbPrecedentSet as kTrue
	End If
End If
If pcTeam<>''
	If lbPrecedentSet
		Sta:and USR_TEAM = @[pcTeam]
	Else
		Sta:where USR_TEAM = @[pcTeam]
		Calculate lbPrecedentSet as kTrue
	End If
End If
End statement

Do $cinst.$executesqlandfetch() Returns lbOK
If lbImmediateSession
	Do $cinst.$SetSession(#NULL,#NULL,kTrue)
End If

Do $cinst.$cols.$findname('DisplayName') Returns loColRef
If loColRef
	While $Loop($cinst)
		Do $cinst.$DisplayName()
	End While
End If

Quit method lbOK